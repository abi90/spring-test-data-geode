buildscript {
  dependencies {
    classpath 'io.spring.gradle:spring-build-conventions:0.0.20.RELEASE'
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
  }
  repositories {
    gradlePluginPortal()
    maven { url 'https://repo.spring.io/plugins-release' }
  }
}

apply plugin: 'io.spring.convention.root'

allprojects {

  group = 'org.springframework.data'

  repositories {

    mavenCentral()

    if (version.contains('-')) {
      maven { url "https://repo.spring.io/milestone" }
    }
    if (version.endsWith('-SNAPSHOT')) {
      maven { url "https://repo.spring.io/snapshot" }
    }
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "minutes"
  }
}

description = 'Spring Test Framework for Apache Geode and VMware Tanzu GemFire using Spring Data.'

ext.releaseBuild = version.endsWith('RELEASE')
ext.snapshotBuild = version.endsWith('SNAPSHOT')
ext.milestoneBuild = !(releaseBuild || snapshotBuild)
